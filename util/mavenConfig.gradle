apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'org.reactorx'
version = libVersion

def mavenRepo = "maven"

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.kotlin.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$rootDir/build/docs/javadoc/$project.name"
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    if (System.getenv("CI")) {
        user = System.getenv("BINTRAY_USER")
        key = System.getenv("BINTRAY_API_KEY")
    } else {
        user = BINTRAY_USER
        key = BINTRAY_API_KEY
    }

    configurations = ['archives']

    pkg {
        repo = mavenRepo
        name = project.archivesBaseName
        publish = true
    }
}