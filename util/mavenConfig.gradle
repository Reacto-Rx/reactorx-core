apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'cz.filipproch.lib'
def mavenRepo = ""

if (project.hasProperty('snapshotBuild')) {
    version = "${libVersion}-SNAPSHOT"
    mavenRepo = 'maven-snapshot'
} else {
    version = libVersion
    mavenRepo = 'maven'
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    if (System.getenv("CI")) {
        user = System.getenv("BINTRAY_USER")
        key = System.getenv("BINTRAY_API_KEY")
    }

    configurations = ['archives']

    pkg {
        repo = mavenRepo
        name = project.archivesBaseName
        publish = true
    }
}